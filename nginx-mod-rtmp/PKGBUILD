# https://gitlab.archlinux.org/archlinux/packaging/packages/nginx
# https://gitlab.archlinux.org/archlinux/packaging/packages/nginx-mod-headers-more
# https://wiki.archlinux.org/title/Creating_packages

# Maintainer: Scott Smith <scott@ssmith.software>

pkgname=nginx-mod-rtmp
pkgrel=1
pkgver=1.2.2

arch=(x86_64)
depends=(glibc)
license=(BSD-2-Clause)
makedepends=(nginx-src)
optdepends=(nginx)
pkgdesc='NGINX-based Media Streaming Server'
url=https://github.com/arut/nginx-rtmp-module

backup=(etc/nginx/modules.d/20-rtmp.conf)
sha256sums=('07f19b7bffec5e357bb8820c63e5281debd45f5a2e6d46b1636d9202c3e09d78')
source=("$url/archive/v$pkgver/rtmp-$pkgver.tar.gz")

prepare () {
	mkdir -p build

	cd build
	ln -sf /usr/src/nginx/auto/ /usr/src/nginx/src/ .
}

build () {
	cd build
	./auto/configure \
		--with-compat \
		--with-ld-opt="$LDFLAGS" \
		--add-dynamic-module="../nginx-rtmp-module-$pkgver"

	make modules
}

package () {
	install -Dm644 "$srcdir/nginx-rtmp-module-$pkgver/LICENSE" \
		"$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	install -dm755 "$pkgdir/etc/nginx/modules.d"

	cd build/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir/usr/lib/nginx/modules/$mod"

		echo "load_module \"/usr/lib/nginx/modules/$mod\";" \
			>>"$pkgdir/etc/nginx/modules.d/20-rtmp.conf"
	done
}
